plugins {
    id "com.android.library"
    id "kotlin-android"
    id "org.jetbrains.kotlin.plugin.serialization"
    id "com.google.protobuf"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/java"
        test.java.srcDirs += "src/test/kotlin"
    }

    kotlinOptions {
        freeCompilerArgs += "-Xuse-experimental=kotlinx.serialization.InternalSerializationApi"
        freeCompilerArgs += "-Xuse-experimental=kotlinx.serialization.ExperimentalSerializationApi"
    }
}

ext {
    tinkVersion = "1.5.0"
    coroutinesVersion = "1.5.0-RC"
    serializationVersion = "1.2.0"
    protobufVersion = "3.16.0"
    datastoreVersion = "1.0.0-beta01"
}

dependencies {
    // Google Tink
    implementation "com.google.crypto.tink:tink-android:$tinkVersion"

    // Kotlinx.Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    // Kotlinx.Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serializationVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-protobuf:$serializationVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-cbor:$serializationVersion"

    // Androidx.DataStore
    implementation "androidx.datastore:datastore:$datastoreVersion"
    implementation "androidx.datastore:datastore-preferences:$datastoreVersion"

    // Google ProtoBuf
    implementation  "com.google.protobuf:protobuf-javalite:$protobufVersion"

    testImplementation "junit:junit:4.13.2"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}